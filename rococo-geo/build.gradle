plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.3'
    id 'io.spring.dependency-management' version '1.1.1'
    id 'com.palantir.docker' version '0.35.0'
}

group 'guru.qa'
version '0.0.1-SNAPSHOT'

dependencies {
    implementation project(':rococo-grpc-common')

    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "net.devh:grpc-server-spring-boot-starter:${project.ext.springGrpcVersion}"
    implementation "org.flywaydb:flyway-core:${project.ext.flywayVersion}"
    implementation "org.flywaydb:flyway-mysql:${project.ext.flywayVersion}"
    annotationProcessor "org.projectlombok:lombok:${project.ext.lombokVersion}"
    compileOnly "org.projectlombok:lombok:${project.ext.lombokVersion}"
    runtimeOnly "mysql:mysql-connector-java:${project.ext.mysqlConnectorVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

docker {
    dependsOn build
    name "onemanpara/rococo-geo:${project.version}"
    tag 'latest', 'onemanpara/rococo-geo:latest'
    files bootJar.archiveFile
    buildArgs([APP_VER: project.version, DOCKER: project.ext.dockerImage])
    noCache true
}

tasks.register('dockerRmi', Exec) {
    workingDir "${buildDir}"
    commandLine 'bash', '-c', "docker rmi -f \$(docker images | grep 'rococo')\n"
}

tasks.named('test') {
    useJUnitPlatform()
}
